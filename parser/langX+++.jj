options {
  STATIC = false;
  DEBUG_LOOKAHEAD = true;
}

PARSER_BEGIN(langX)
package parser;
import java.io.*;
 

public class langX {
final static String Version = "X++ Compiler - Version 1.0 - 2004";
int contParseError = 0;           // contador de erros sintáticos


// Define o método "main" da classe langX.  
  public  static void main(String args[]) throws ParseException 
  {
  boolean debug = false;

    String filename = ""; // nome do arquivo a ser analisado
    langX parser;     // analisador léxico/sintático
    int i;
    boolean ms = false;

    System.out.println(Version);
    // lê os parâmetros passados para o compilador
    for (i = 0; i < args.length - 1; i++)
    {
    if (args[i].equals("-debug_AS") )
            debug = true;
    else
        {
            System.out.println("Usage is: java langX [-debug_AS] inputfile");
            System.exit(0);
        }
    }

    if (args[i].equals("-"))
    {       // lê da entrada padrão      
        System.out.println("Reading from standard input . . .");
        parser = new langX(System.in); // cria AS
     }
     else 
    {       // lê do arquivo
        filename = args[args.length-1];  
        System.out.println("Reading from file " + filename + " . . .");
        try {  // cria AS
            parser = new langX(new java.io.FileInputStream(filename));
        } 
    catch (java.io.FileNotFoundException e) {
               System.out.println("File " + filename + " not found.");
        return;
        }
    } 

    if (! debug) parser.disable_tracing(); // desabilita verbose do AS

    try {
        parser.program();   // chama o método que faz a análise
    }
    catch (ParseException e)
    {
    System.err.println(e.getMessage());
    parser.contParseError = 1;   // não existe recuperação de erros
    }
    finally {
        System.out.println(parser.token_source.foundLexError() +
                                " Lexical Errors found");
        System.out.println(parser.contParseError + " Syntactic Errors found");
     }

}  // main

static public String im(int x)
{
int k;
String s;
   s = tokenImage[x];
   k = s.lastIndexOf("\"");
   try {s = s.substring(1,k);}
   catch (StringIndexOutOfBoundsException e)
   {}
   return s;
}

}  // langX

PARSER_END(langX)

TOKEN_MGR_DECLS :
{
int countLexError = 0;

public int foundLexError()
{
   return countLexError;
}

}


/* Espacos a serem desprezados no inicio de cada token */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SKIP :
{
  "/*" : multilinecomment
}

SKIP :
{
  "//" : singlelinecomment
}

<multilinecomment> SKIP:
{
   "*/" : DEFAULT
|  <~[]>
}

<singlelinecomment> SKIP:
{
   <["\n","\r"]> : DEFAULT
|   <~[]>
}


/* Palavras reservadas */

TOKEN :
{
  < BREAK: "break" > 
| < CLASS: "class" >
| < CONSTRUCTOR: "constructor" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FOR: "for" >
| < IF: "if" >
| < INT: "int" >
| < NEW: "new" >
| < PRINT: "print" >
| < READ: "read" >
| < RETURN: "return" >
| < STRING: "string" >
| < SUPER: "super" >
//mudancas
| < WHILE: "while" >
| < DO: "do" >
| < SWITCH: "switch" >
| < CASE: "case" >
| < DEFAULTT: "default" >
| < CHAR: "char" >
| < BOOLEAN: "boolean" >
| < DOUBLE: "double" >
| < BYTE: "byte" >
| < SHORT: "short" >
| < LONG: "long" >
| < FLOAT: "float" >
| < FINAL: "final" >
| < PUBLIC: "public" >
| < PRIVATE: "private" >
| < PROTECTED: "protected" >

}

/* constantes */

TOKEN :
{
  < int_constant:(  // números decimais
                  (["0"-"9"] (["0"-"9"])* )
                 ) >
|
  < string_constant:    // constante string como "abcd bcda"
      "\""( ~["\"","\n","\r"])* "\"" >
|
  < null_constant: "null" > // constante null
| 
  < boolean_constant: "true" | "false" > //valores booleanos
|
  < char_constant: "'" (["0"-"9"] | ["a"-"f"] | ["A"-"F"]) "'" >
|
  < double_constant: (<int_constant> "." ( <int_constant> ( ("e" | "E")? ("-")? <int_constant> )? )? | (<int_constant>)? "." <int_constant> (("e" | "E")? ("-")? <int_constant>)? ) ("D" | "d")? >
| 
  < byte_constant: ("-")? (<DIGIT> (<DIGIT>)? | ( ["1"]["0"-"1"] ["0"-"9"]) (["1"] ["0"-"2"] ["0"-"7"])) >
|
  < short_constant: ("-")? ((<DIGIT> (<DIGIT>)? (<DIGIT>)? (<DIGIT>)?) | 
                            (["1"-"2"] <DIGIT> <DIGIT> <DIGIT> <DIGIT>) |
                            (["3"] ["0"-"1"] <DIGIT> <DIGIT> <DIGIT>) |
                            (["3"] ["2"] ["0"-"6"] <DIGIT> <DIGIT>) |
                            (["3"] ["2"] ["7"] ["0"-"5"] <DIGIT>) |
                            (["3"] ["2"] ["7"] ["6"] ["0"-"7"])
  ) >
|
  < long_constant: <DIGIT> (<DIGIT>)* ("l" | "L") >
|
  < float_constant: (<int_constant> "." ( <int_constant> ( ("e" | "E")? ("-")? <int_constant> )? )? | (<int_constant>)? "." <int_constant> (("e" | "E")? ("-")? <int_constant>)? ) ("F" | "f")? >

}

/* Identificadores */

TOKEN :
{
  < IDENT: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:["A"-"Z","a"-"z"] >
|
  < #DIGIT:["0"-"9"] >
}

/* Simbolos especiais */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
//mudanca
| < DOISP: ":" >
}

/* Operadores */

TOKEN :
{
  < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NEQ: "!=" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < REM: "%" >
//mudancas
| < AND: "&&" >
| < OR: "||" >
| < XOR: "^" >
| < NOT: "!" >
}

/* Trata os erros léxicos */
SPECIAL_TOKEN :
{
<INVALID_LEXICAL: 
(~ ["a"-"z", "A"-"Z",
  "0"-"9",
  "\"",
  "(",
  ")",
  "[",
  "]",
  "{",
  "}",
  ";",
  ",",
  ".",
  "=",
  ">",
  "<",
  "!",
  "+",
  "-",
  "*",
  "/",
  "%",
  " ",
  "\t",
  "\n",
  "\r",
  "\f",
  //mudancas
  ":",
  "_",
  "'",
  "|",
  "&",
  "^"

])+>
   {
    System.err.println("Line " + input_stream.getEndLine() + 
                           " - Invalid string found: " + image);
    countLexError++;
   }
|
   <INVALID_CONST: 
   "\"" (~ ["\n","\r","\""])* ["\n","\r"]>
   {
    System.err.println("Line " + input_stream.getEndLine() + 
                           " - String constant has a \\n: " + image);
    countLexError++;
   }

}



void program() :
{
}
{
    [ classlist() ] <EOF>
}

void classlist(): 
{
}
{
   classdecl() [ LOOKAHEAD(2) classlist() ]
}


void classdecl():
{
}
{
   [<PUBLIC> | <PRIVATE> | <PROTECTED>] <CLASS>  <IDENT> [ <EXTENDS> <IDENT> ] classbody() 
}

void classbody():
{                         
}
{
   <LBRACE>
      [LOOKAHEAD(2)classlist()]
      (LOOKAHEAD(3) vardecl() <SEMICOLON>)*
      (LOOKAHEAD(2) constructdecl())*
      (methoddecl())* 
   <RBRACE>
}


void vardecl():
{
}
{
	[<PUBLIC> | <PRIVATE> | <PROTECTED>] 
	[<FINAL>]
   (primitivetype() | <IDENT>) 
   <IDENT> ( <LBRACKET> <RBRACKET>)* 
  (<COMMA> <IDENT> ( <LBRACKET> <RBRACKET>)* )*  
}

void constructdecl():
{
}
{
	[<PUBLIC> | <PRIVATE> | <PROTECTED>] 
    <CONSTRUCTOR> methodbody()

}


void methoddecl():
{
}
{	
	[<PUBLIC> | <PRIVATE> | <PROTECTED>] 
	[<FINAL>]
   (primitivetype() | <IDENT>) (<LBRACKET> <RBRACKET>)* 
   <IDENT> methodbody()
}

void methodbody():
{
}
{
    <LPAREN> paramlist() <RPAREN> statement()
}

void paramlist():
{
}
{
   [
    (primitivetype() | <IDENT>) <IDENT> (<LBRACKET> <RBRACKET>)* 
    (<COMMA> (primitivetype() | <IDENT>) <IDENT> (<LBRACKET> <RBRACKET>)* )*
   ]
}

void statement():
{
}
{
    LOOKAHEAD(2)
    vardecl() <SEMICOLON>
|
	LOOKAHEAD(3)
    atribstat() <SEMICOLON> 
|
    printstat() <SEMICOLON>
|
    readstat() <SEMICOLON>
|
    returnstat() <SEMICOLON>
|
    superstat() <SEMICOLON>
|
    ifstat() 
|
    forstat() 
|
    <LBRACE> statlist() <RBRACE> 
|
    <BREAK> <SEMICOLON>
|
    <SEMICOLON>
//mudancas
|	
	methodcall() <SEMICOLON>
|
	dowhilestat() <SEMICOLON>
|
	whilestat()
|
	switchstat()

}

//mudanca
void methodcall():
{
	
}
{
	<IDENT> (<DOT> <IDENT>)* <LPAREN> paramlist() <RPAREN>
}

void atribstat():
{
}
{
    lvalue() <ASSIGN> ( alocexpression() | LOOKAHEAD(3) expression() | LOOKAHEAD(3) methodcall() | LOOKAHEAD(3) logicexpression())
}

void printstat():
{
}
{
    <PRINT> (LOOKAHEAD(3) expression() | logicexpression())
}

void readstat():
{
}
{
    <READ> lvalue()
}


void returnstat():
{
}
{  
    <RETURN> [LOOKAHEAD(3) expression() | logicexpression()]
}


void superstat():
{
}
{
    <SUPER> <LPAREN> arglist() <RPAREN>
}

void ifstat():
{
}
{
    <IF> <LPAREN> (LOOKAHEAD(3) expression() | logicexpression()) <RPAREN> statement()
    [LOOKAHEAD(2) <ELSE> statement()]
}



void forstat():
{
}
{
    <FOR> <LPAREN> [atribstat()] <SEMICOLON>
                   [LOOKAHEAD(3) expression() | logicexpression()] <SEMICOLON>
                   [atribstat()] <RPAREN>
                    statement()
}

void whilestat():
{
	
}
{
	<WHILE> <LPAREN> (LOOKAHEAD(3) expression() | logicexpression()) <RPAREN> statement()
}

void dowhilestat():
{
	
}
{
	<DO> statement() <LPAREN> (LOOKAHEAD(3) expression() | logicexpression())<RPAREN>
}

void switchstat():
{
	
}
{
	<SWITCH> <LPAREN> <IDENT> <RPAREN> <LBRACE> (switchcasestat())* <RBRACE>
}

void switchcasestat():
{
	
}
{
	((<CASE> factor()) | <DEFAULTT>) <DOISP> statement()
}

void  statlist() :
{
}
{
   statement() [statlist()]
}

void lvalue() :
{
}
{
   <IDENT> (
           <LBRACKET> expression() <RBRACKET> |
           <DOT> <IDENT> [<LPAREN> arglist() <RPAREN>]
         )*
}

void alocexpression() :
{
}
{
    <NEW> ( 
             LOOKAHEAD(2) <IDENT> <LPAREN> arglist() <RPAREN> | 
             ( primitivetype() | <IDENT> )  
             (<LBRACKET> expression() <RBRACKET>)+
          )
} 


void expression() :
{
}
{	
    numexpr() [( <LT> | <GT> | <LE> | <GE> | <EQ> | <NEQ>) numexpr()]
}

void logicexpression():
{
	
}
{
	logicterm() ((<OR> | <XOR>) logicterm())*
}

void logicterm():
{
}
{
    logicunaryexpr() ((<AND>) logicunaryexpr())*
}

void logicunaryexpr() :
{
}
{
   [<NOT>] logicfactor()
}

void logicfactor():
{
	
}
{
	(
	<boolean_constant> |
       LOOKAHEAD(3) methodcall() |
       lvalue() |
       <LPAREN> expression() <RPAREN>)
}

void numexpr():
{
}
{
    term() ((<PLUS> | <MINUS>) term())*
}

void term():
{
}
{
    unaryexpr() ((<STAR> | <SLASH>| <REM>) unaryexpr())*
}

void unaryexpr() :
{
}
{
   [(<PLUS> | <MINUS>)] factor()
}




void factor():
{
}
{   
    ( 
       <int_constant> | 
       <string_constant> |
       <boolean_constant> |
       <char_constant> |
       <double_constant> |
       <byte_constant> |
       <short_constant> |
       <long_constant> |
       <float_constant> |
       <null_constant> |
       LOOKAHEAD(3) lvalue() |
       methodcall() |
       <LPAREN> expression() <RPAREN>)
}

void arglist():
{
}
{
    [expression() (<COMMA> expression())*]
}

void primitivetype():
{
	
}
{
	(<INT> | <STRING> | <BOOLEAN> | <CHAR> | <DOUBLE> | <BYTE> | <SHORT> | <LONG> | <FLOAT>)
}